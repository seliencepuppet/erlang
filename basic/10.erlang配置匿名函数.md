
### 使用fun() 创建匿名函数

<br/>

fun就是匿名函数. 被称为匿名函数, 是因为它并没有名字. 我们来做点试验, 先定义一个fun, 然后把它赋给一个变量Z:

```erl
1> Z = fun(X) -> (2 * X) end.
#Fun<erl_eval.6.56006484>
```

当定义一个fun时, Erlang shell会打印#Fun<...>, 这里的... 常常是一些奇怪的数字. 不过现在不用管它.

我们可以将一个参数应用到一个fun上, 对于fun来说, 这是我们唯一能做的事, 像这样: 

```erl
2> Z(2).
4
3> Z(3).
6
```

对这个fun来说, Z显然不是一个好名字, 叫做Double可能更好些, 这恰好表述了fun的功能:

```erl
3> Double = Z.
#Fun<erl_eval.6.10732646>
4> Double(4).
8
```

fun可以拥有任意数量的参数. 我们可以像下面一样, 编个函数来计算直角三角形的斜边:

```erl
5> Hypot = fun(X,Y) -> math:sqrt(X * X + Y * Y) end.
#Fun<erl_eval.12.115169474>
6> Hypot(3,4).
5.00000
```

如果调用参数的个数不正确, 会得到一个错误:

```erl
7> Hypot(3).
** exited: #Fun<erl_eval.12.115169474>
```

这个错误为何被称为birthday 一个函数可以接受的参数个数. badarity表明在Erlang中找不到由所调用的函数名及其给定的参数数量所表明的函数. 在这个例子中, 给出了函数Hypot, 它需要两个参数, 但我们只传了一个.

fun也可以有若干个不同的子句. 下面是在华氏气温和摄氏气温之间进行转换的函数:

```erl
8> TempConvert = fun({c,C}) -> {f, 32 + C * 9 / 5};
8>                  ({f,F}) -> {c, (F - 32) * 5 / 9}
8>               end.
#Fun<erl_eval.6.56006484>
9> TempConvert({c,100}).
{f,212.000}
10> TempConvert({f,212}).
{c,100.000}
11> TempConvert({c,0}).
{f,32.0000}
```

说明: 第8行的表达式跨越了好几行, 在输入这个表达式时, 没输入一个新行shell都会重复打印 "8>", 这意味着这个表达式并未结束, shell还在等待后续的输入.

Erlang是一种函数式的编程语言, 也就是说, 除了极个别的情况外, fun既可以作为函数的参数, 也可以作为函数 (或者fun) 的结果.

这些能够返回fun或接受fun作为参数的函数, 都被称作高阶函数 (high-order function). 

<br/>

### 以fun为参数的函数

<br/>

list是标准库中的一个模块, 从中导出的很多函数都是以fun作为参数的. 其中, 最有用的是 lists.map(F,L). 这个函数将fun F应用到列表L的每一个元素上, 并返回一个新的列表.

```erl
10> Z = fun(X) -> (2 * X) end.
#Fun<erl_eval.6.56006484>
11> Double = Z.
#Fun<erl_eval.6.10732646>
12> L = [1,2,3,4].
[1,2,3,4]
12> lists:map(Double, L).
[2,4,6,8].
```

另一个常用的函数是lists.filter(P,L), 它返回一个新列表, 新列表由列表L中每一个能满足P(E)为true的元素组成.

让我们定义一个函数Even(X), 当X为奇数则返回true.

```erl
14> Even = fun(X) -> (X rem 2) =:= 0 end.
#Fun<erl_eval.6.56006484>
```

其中X rem 2是对X除2取余数, 而=:=是一个恒等测试符号. 现在我们可以测试一下Even, 然后用它作为map和filter的参数.

```erl
15> Even(8).
true
16> Even(7).
false
17> lists:map(Even, [1,2,3,4,5,6,8]).
[false,true,false,true,false,true,true]
18> lists:filter(Even, [1,2,3,4,5,6,8]).
[2,4,6,8]
```

我们将像map和filter这样在一个函数调用中处理整个列表的操作称为list-at-a-time操作.

list-at-a-time操作能让程序变得简洁易懂, 有了它, 我们就可以把处理整个列表的程序看作是一个的抽象步骤. 这就是我们的程序能变得更为精炼的原因. 否则, 我们必须将处理列表元素的操作分解为一系列独立的步骤.

## 返回fun的函数

fun不仅可以用作函数的参数 (比如map和filter), 而且其他函数也可以将fun当作返回值. 下面是一个例子, 假设有一个名为fruit的列表:

```erl
1> Fruit = [apple,pear,orange].
[apple,pear,orange]
2> MakeTest = fun(L) -> (fun(X) -> lists:member(X,L) end) end.
#Fun<erl_eval.6.56006484>
3> IsFruit = MakeTest(Fruit).
#Fun<erl_eval.6.56006484>
```

如果X是列表L的成员, 那么函数lists:member(X,L) 返回true, 反之则返回false. 现在我们已经编写了一个测试函数, 可以进行一些测试.

```erl
4> IsFruit(pear).
true
5> IsFruit(apple).
true
6> IsFruit(dog).
false
```

同样, 我们还能将其用作lists:filter/2的参数:

```erl
7> lists.filter(IsFruit, [dog,orange,cat,apple,bear]).
[orange,apple]
```

返回fun的fun, 这个语法多少让人迷惑, 下面花一点时间想想, 一个返回"正常"值得fun一般是这样的:

```erl
1> Double = fun(X) -> (2 * X) end.
#Fun<erl_eval.6.56006484>
2> Double(5).
10
```

括号之中的代码 (更明确地说, 也就是2 * X)很明显就是函数的返回值. 现在我们试着把这个fun放进括号之中. 请记住, 括号之中的东西就是返回值:

```erl
3> Mult = fun(Times) -> ( fun(X) -> X * Times end ) end.
#Fun<erl_eval.6.56006484>
```

现在, 括号之中的fun就是fun(X) -> X * Times end, 这是关于X的一个函数, 但Times是哪里冒出来的? 答案就是, 它是更外层的fun的参数.

对Mult(3)求值返回fun(X) -> X * 3 end, 也就是内层fun的函数体, 其中Times为3所替代, 现在进行测试

```erl
4> Triple = Mult(3).
#Fun<erl_eval.6.56006484>
5> Triple(5).
15
```

因此, Mult是Double的一个泛化版本. 它并非计算一个值然后返回它, 而是会返回一个函数, 这个函数只有在被调用的时候才会计算具体的值.

## 定义你自己的抽象流程控制

到目前为止, 还没有涉及到if, switch, for, while这些语句, 而且似乎也没有发现有何不妥, 原本应当出现这些语句的位置现在都被模式匹配和高阶函数替代了. 到目前为止, 我们不需要任何额外的控制结构.

Erlang 没有for循环, 但是可以自我实现一个:

```lib.misc.erl
for(Max, Max, F) -> [F(Max)];
for(I, Max, F) -> [F(I)|for(I+1, Max, F)].
```

就这么简单, 例如, 对for(1,10,F)求值会生成列表[ F(1), F(2), ..., F(10) ]. 

这个for循环中的模式匹配是如何工作的呢? 第一个子句仅当第1个参数和第2个参数相等时才会匹配. 那么, 在调用for(10,10,F)时, 第一个子句就会将Max绑定到10, 结果就是列表 [F(10)]. 而如果调用参数是for(1,10,F), 第一个子句不会匹配, 因为Max无法同时匹配成1和10. 这时, 得到匹配的第二个子句, 这个子句返回[F(I)|for(I+1,10,F)], 此时变量绑定为 I->1, Max->10, 也就是I被1替换, Max被10替换, 结果就是[F(1)|for(2,10,F)] .

## 何时使用高阶函数

使用高阶函数可以创建自己的新的抽象控制结构. 可以将函数作为函数的参数传入, 可以编写返回fun的函数. 但在实践中, 这些技巧并不常用.

1) 实际上, 我写的所有模块都会用到类似于lists:map/2这样的函数, 它如此通用以至于我几乎要把Map当作是Erlang语言的一部分, 调用类似map, filter, partition 这些lists模块中的函数特别常见.
2) 有时, 我也创建自己的抽象控制结构, 但这远不如调用标准库模块中的高阶函数频繁, 在一些大的模块中, 也只是偶尔才会用到这种技术.
3) 编写返回fun的函数很不常见.

现在我们有了一个简单的for循环, 可以用它去创建一个从1到10的整数列表:

```erl
1> lib_misc:for(1,10,fun(I) -> I end).
[1,2,3,4,5,6,7,8,9,10]
```

或者用它去计算从1到10的整数的平方

```erl
2> lib_misc:for(1,10,fun(I) -> I * I end).
[1,4,9,16,25,36,49,64,81,100]
```

随着经验的不断积累, 创建自己的控制结构降低程序的代码量, 让程序变得更为清晰. 根据自己的需求量身定做最为合适的控制结构, 超越琐碎呆板的控制结构, 从语言的桎梏中解脱.



































